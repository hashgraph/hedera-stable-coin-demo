buildscript {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.glassfish.jaxb:jaxb-runtime:2.3.2"
        classpath "org.postgresql:postgresql:42.2.14"
        classpath "io.github.jklingsporn:vertx-jooq-generate:5.2.0"
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id "org.flywaydb.flyway" version "6.5.5"
    id "com.google.protobuf" version "0.8.12" apply false
    id 'nu.studer.jooq' version '5.0.1'
}


import groovy.xml.MarkupBuilder
import org.jooq.codegen.GenerationTool

import javax.xml.bind.JAXB

group = "com.hedera.hashgraph.stablecoin.platform"
version = "1.0.0"
description = "Hederaâ„¢ Hashgraph Stable Coin Platform"

allprojects {
    apply plugin: "java"

    sourceCompatibility = 14
    targetCompatibility = 14

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/ethereum/maven/" }
    }

    dependencies {
        implementation 'com.google.guava:guava:29.0-jre'
    }
}

dependencies {
    implementation project(":proto")
    implementation project(":sdk")
    implementation "io.vertx:vertx-web:3.9.2"
    implementation "io.vertx:vertx-pg-client:3.9.2"
    implementation 'com.hedera.hashgraph:sdk:1.1.0'
    implementation 'io.github.cdimascio:java-dotenv:5.2.1'
    implementation 'io.grpc:grpc-netty-shaded:1.30.2'
    implementation 'org.jooq:jooq:3.13.4'
    implementation 'org.jooq:jooq-meta:3.13.4'
    implementation "io.github.jklingsporn:vertx-jooq-classic-reactive:5.2.0"
    implementation "org.web3j:abi:5.0.0"
    implementation "org.web3j:contracts:5.0.0"
    implementation "org.web3j:core:5.0.0"
    // flyway
    compile "org.flywaydb:flyway-core:7.11.2"

    // jOOQ
    implementation 'org.jooq:jooq:3.13.4'
    implementation 'org.jooq:jooq-meta:3.13.4'
    implementation 'org.postgresql:postgresql:42.2.14'

    // jOOQ database driver
    jooqGenerator 'org.postgresql:postgresql:42.2.14'
}

flyway {
    def dbURL = System.getenv("PLATFORM_DATABASE_URL") ?: "postgresql://localhost:5432/"
    def dbSCHEMA = System.getenv("PLATFORM_DATABASE_DB") ?: "stable_coin_platform"
    def dbUSER = System.getenv("PLATFORM_DATABASE_USERNAME")
    def dbPASS = System.getenv("PLATFORM_DATABASE_PASSWORD")

    url = "jdbc:${dbURL}${dbSCHEMA}"
    user = "${dbUSER}"
    password = "${dbPASS}"
    locations = ['filesystem:src/main/resources/migrations']
}

task jooqGenerate {
    doLast() {
        def writer = new StringWriter()

        def dbURL = System.getenv("PLATFORM_DATABASE_URL") ?: "postgresql://localhost:5432/"
        def dbSCHEMA = System.getenv("PLATFORM_DATABASE_DB") ?: "stable_coin"
        def dbUSER = System.getenv("PLATFORM_DATABASE_USERNAME")
        def dbPASS = System.getenv("PLATFORM_DATABASE_PASSWORD")

        new MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.13.0.xsd') {
                jdbc {
                    driver('org.postgresql.Driver')
                    url("jdbc:${dbURL}${dbSCHEMA}")
                    user("${dbUSER}")
                    password("${dbPASS}")
                }
                generator {
                    name('io.github.jklingsporn.vertx.jooq.generate.classic.ClassicReactiveVertxGenerator')
                    database {
                        name('org.jooq.meta.postgres.PostgresDatabase')
                        inputSchema('public')
                    }
                    generate([:]) {
                        deprecated(false)
                        records(false)
                        interfaces(false)
                        fluentSetters(false)
                        pojos(false)
                        daos(false)
                    }
                    target() {
                        packageName('com.hedera.hashgraph.stablecoin.platform.db')
                        directory("$projectDir/src/main/java")
                    }
                    strategy {
                        name('io.github.jklingsporn.vertx.jooq.generate.VertxGeneratorStrategy')
                    }
                }
            }

        GenerationTool.generate(
            JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.meta.jaxb.Configuration.class)
        )
    }
}

application {
    mainClassName = 'com.hedera.hashgraph.stablecoin.platform.PlatformVerticle'
}

jar {
    enabled = false
    manifest {
        attributes 'Main-Class': 'com.hedera.hashgraph.stablecoin.platform.PlatformVerticle'
    }
}

tasks.shadowJar.configure {
    classifier = null
}
